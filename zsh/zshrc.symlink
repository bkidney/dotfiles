# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/projects

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
if [[ -z $TMUX ]]; then
    for file in ${(M)config_files:#*/path.zsh}
    do
        source $file
    done
fi

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# History settings
HISTSIZE=1000
SAVEHIST=1000
HISTFILE=~/.history
setopt APPEND_HISTORY
setopt EXTENDED_HISTORY
setopt HIST_IGNORE_DUPS
setopt HIST_ALLOW_CLOBBER
setopt HIST_REDUCE_BLANKS
setopt INC_APPEND_HISTORY 
setopt SHARE_HISTORY
setopt HIST_IGNORE_SPACE

export CLICOLOR=1
[[ "$TERM" == "xterm" ]] && export TERM=xterm-256color

# Load antigen bundles

antigen use oh-my-zsh

antigen bundles <<END
	zsh-users/zsh-autosuggestions
	zsh-users/zsh-completions
	jreese/zsh-titles
	colored-man-pages
	zsh-users/zsh-syntax-highlighting
	joel-porquet/zsh-dircolors-solarized
END

# antigen theme sobolevn/sobole-zsh-theme
antigen theme denysdovhan/spaceship-prompt spaceship 

antigen apply

ZSH_HIGHLIGHT_STYLES[default]='fg=cyan,bold' #base1
ZSH_HIGHLIGHT_STYLES[alias]='fg=white'
ZSH_HIGHLIGHT_STYLES[builtin]='fg=yellow'
ZSH_HIGHLIGHT_STYLES[function]='fg=white'
ZSH_HIGHLIGHT_STYLES[command]='fg=white'
ZSH_HIGHLIGHT_STYLES[precommand]='fg=white'
ZSH_HIGHLIGHT_STYLES[commandseparator]='fg=green,bold' #base01
ZSH_HIGHLIGHT_STYLES[path]='fg=cyan'
ZSH_HIGHLIGHT_STYLES[single-hyphen-option]='fg=blue,bold' #base0
ZSH_HIGHLIGHT_STYLES[double-hyphen-option]='fg=blue,bold' #base0
ZSH_HIGHLIGHT_STYLES[back-quoted-argument]='fg=red,bold' #orange

SPACESHIP_GIT_STATUS_SHOW=false

if [[ $IS_FREEBSD ]]; then
	unalias ls
fi
